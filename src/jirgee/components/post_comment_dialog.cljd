(ns jirgee.components.post-comment-dialog
  "Single-field comment dialog with Mongolian keyboard (same layout as feedback_dialog)."
  (:require
   [cljd.flutter :as f]
   ["package:flutter/material.dart" :as m]
   [clojure.string :as str]
   [virtual-keyboard.keyboard :as keyboard]
   [components.tokens :as tokens]
   [components.dialog :as dialog]
   [components.text :as text]
   ;; text-field from ../mgl-components (amjil/mgl-components)
   [components.text-field :as text-field]))

(defn post-comment-dialog
  "Dialog with one content field + Mongolian keyboard. on-submit receives the content string."
  [{:keys [on-submit on-cancel]}]
  (let [content-atom (atom "")]
    (f/widget
     :managed [content-ctrl (m/TextEditingController.)
               focus-node (m/FocusNode)]
     :get {{{:flds [surfaceVariant onSurface]} .-colorScheme
            {:flds [titleLarge bodyMedium]} .-textTheme} m/Theme}
     (dialog/vertical-custom-dialog
      {:width-factor 1
       :height-factor 0.95
       :inset-padding m/EdgeInsets.zero
       :child
       (m/Column
        .children
        [(m/Expanded
          .child
          (m/Stack
           .children
           [(m/Row
             .crossAxisAlignment m/CrossAxisAlignment.stretch
             .children
             [(m/Container
               .alignment m/Alignment.topCenter
               .padding (m/EdgeInsets.only .top (tokens/space :l) .left (tokens/space :m) .right (tokens/space :m))
               .decoration (m/BoxDecoration
                            .border (m/Border .right (m/BorderSide .color surfaceVariant .width 1.5)))
               .child (text/text-block
                       {:text "Comment"
                        :style (.copyWith titleLarge
                                         .fontWeight m/FontWeight.bold
                                         .color onSurface)}))

              (m/Expanded
               .child
               (m/Align
                .alignment m/Alignment.topLeft
                .child
                (m/Padding
                 .padding (m/EdgeInsets.all (tokens/space :l))
                 .child
                 (text-field/text-field
                  {:controller content-ctrl
                   :autofocus true
                   :focus-node focus-node
                   :hint "Comment..."
                   :max-lines nil
                   :style (.copyWith bodyMedium
                                     .fontSize 18.0
                                     .color (.withOpacity onSurface 0.6))
                   :on-changed #(swap! content-atom (constantly %))}))))])

            (m/Align
             .alignment m/Alignment.topRight
             .child (m/IconButton
                     .icon (m/Icon m/Icons.close)
                     .onPressed on-cancel))

            (f/widget
             :watch [content content-atom]
             (m/Align
              .alignment m/Alignment.bottomRight
              .child (m/Padding
                      .padding (m/EdgeInsets.all (tokens/space :m))
                      .child (m/IconButton
                              .icon (m/Icon m/Icons.send)
                              .color (if (not (str/blank? content))
                                       m/Colors.blueAccent
                                       (.withOpacity onSurface 0.3))
                              .onPressed (when (not (str/blank? content))
                                           #(on-submit (str/trim @content-atom)))))))

            (keyboard/candidates nil)]))

         (m/Container
          .decoration (m/BoxDecoration
                       .border (m/Border .top (m/BorderSide .color surfaceVariant .width 0.5)))
          .child (keyboard/keyboard {}))])}))))
