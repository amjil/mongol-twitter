(ns jirgee.services.auth
  (:require
   [jirgee.services.user :as user-service]
   [jirgee.states.global :as gs]
   [jirgee.common.utils :as utils]
   [jirgee.common.response :as response]
   [jirgee.services.shared-preferences :as sp]

   ["package:flutter/widgets.dart" :as widgets]
   ["package:http/http.dart" :as http]
   ["dart:convert" :as convert]))

(defn login [ctx ^widgets/NavigatorState navigator info right]
  (utils/show-indicator ctx)
  (let [result (await
                (http/post (Uri/http utils/base-url "/api/auth/login")
                           .headers {"Content-Type" "application/json; charset=UTF-8"}
                           .body
                           (convert/jsonEncode
                            info)))]
    (response/response-message
     ctx
     result
     (fn [x]
       (user-service/save-token (get x "token"))
       (user-service/save-info (get x "info"))
       (.popUntil navigator (widgets.ModalRoute/withName "/"))))))

(defn signup [ctx ^widgets/NavigatorState navigator info]
  (utils/show-indicator ctx)
  (if (not= (get info "password")
            (get info "password_confirmation"))
    (utils/show-toast ctx "Password confirmation")
    (let [result (await
                  (http/post (Uri/http utils/base-url "/api/auth/signup")
                             .headers {"Content-Type" "application/json; charset=UTF-8"}
                             .body
                             (convert/jsonEncode
                              info)))]
      (response/response-message
       ctx
       result
       (fn [_] (.pushNamed navigator "/login"))))))

(defn get-token []
  (stream
   (map (fn [result]
          (swap! gs/state assoc :user-token result)
          :ok-token))
   (map (fn [[er st]]
          :error-token))
   :as-values
   (.asStream (sp/get-value {:type :string :key "user-token"}))))