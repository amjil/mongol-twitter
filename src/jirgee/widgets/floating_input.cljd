(ns jirgee.widgets.floating-input
  (:require 
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter.alpha2 :as f]
   [virtual-keyboard.keyboard :as keyboard]
   [virtual-keyboard.options :as keyboard-options]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [menu-bar.menu :as menu]
   
   [jirgee.widgets.keyboard :as key-widgets]
   [jirgee.states.global :as gs]
   [jirgee.common.theme :as theme]
   [jirgee.common.utils :as utils]))

(defn input-box []
  (f/widget
   :context ctx
   :get [:info]
   :let [w (-> m/MediaQuery (.of ctx) .-size .-width)]
   :watch [{candidates-list :keyboard/candidates-list} gs/state]
   (m/Column
    .crossAxisAlignment m.CrossAxisAlignment/stretch
    .children
    [(m/Expanded
      .child 
      (m/Stack
       .children
       [(m/Container
         .width double/infinity
         .height double/infinity
         .child
         (m/GestureDetector
          .child
          (m/Container
           .color m.Colors/transparent
           .width double/infinity
           .height double/infinity)
          .onTap (fn []
                   (swap! gs/state assoc :floating-input false))))
        (m/Container
         .padding (.all m/EdgeInsets 5)
         .decoration (m/BoxDecoration
                      .color theme/grey-color
                      .borderRadius
                      (.all m/BorderRadius (.circular m/Radius 10))
                      .border (.all m/Border .width 0.8))
         .constraints (m/BoxConstraints
                       .minWidth 100
                       .maxWidth (/ w 2))
         .child
         (mgl/MongolTextField
          .controller (get @gs/state :keyboard/text-controller)
          .style (m/TextStyle .fontSize 22)
          .decoration (m/InputDecoration
                       .border m.InputBorder/none)
          .showCursor true
          .readOnly true
          .maxLines nil))
        (m/Positioned
         .bottom 10
         .right 0
         .left 0
         .child
         (menu/menu {:bar {:elavation 10
                           :background-color
                           (-> m/Theme (.of ctx) .-colorScheme .-tertiaryContainer)}
                     :item {:on-tap (fn [x] (keyboard-action/on-candidates-clicked x gs/state info))}}
                    candidates-list))]))
     (m/Container
      .color (-> m/Theme (.of ctx) .-colorScheme .-secondaryContainer)
      .child
      (m/IntrinsicHeight
       .child
       (keyboard/keyboard)))])))