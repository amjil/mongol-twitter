(ns jirgee.screens.lists
  (:require [cljd.flutter :as f]
            ["package:flutter/material.dart" :as m]
            ["package:mongol/mongol.dart" :as mgl]
            [jirgee.components.mongol-side-layout :refer [mongol-side-layout]]
            [components.dialog :as dialog]
            [components.text-field :as text-field]
            [jirgee.services.list :as list-service]
            [jirgee.state :refer [lists-state-atom
                                  lists-loading?-atom
                                  lists-create-name-ctrl-atom
                                  lists-create-desc-ctrl-atom
                                  lists-create-is-public?-atom
                                  set-lists-state!
                                  set-lists-loading?!
                                  set-lists-create-name-ctrl!
                                  set-lists-create-desc-ctrl!
                                  set-lists-create-is-public?!]]
            [pull-to-refresh.refresh :refer [horizontal-refresh]]))

(defn- list-item [list-data]
  (f/widget
   :context ctx
   :get {{{:flds [surfaceVariant onSurface]} .-colorScheme
          {:flds [titleMedium bodySmall]} .-textTheme} m/Theme}
   :let [name (get list-data "name" "")
         description (get list-data "description" "")
         members-count (get list-data "members_count" 0)
         is-public (get list-data "is_public" false)]
   (m/ListTile
    .leading (m/Icon m/Icons.list)
    .title (mgl/MongolText name .style titleMedium)
    .subtitle (mgl/MongolText (str description " Â· " members-count " members") .style bodySmall)
    .trailing (m/Row
               .mainAxisSize m/MainAxisSize.min
               .children
               [(when is-public (m/Icon m/Icons.public .size 16.0))
                (m/IconButton
                 .icon (m/Icon m/Icons.arrow_forward_ios .size 16.0)
                 .onPressed #(println "Navigate to list:" (get list-data "id")))])
    .onTap #(println "Open list:" (get list-data "id")))))

(defn lists-screen []
  (f/widget
   :context ctx
   :watch [lists lists-state-atom
           is-loading? lists-loading?-atom
           name-ctrl lists-create-name-ctrl-atom
           desc-ctrl lists-create-desc-ctrl-atom
           public? lists-create-is-public?-atom]
   :get {{{:flds [surface primary]} .-colorScheme} m/Theme}
   :let [load-lists! (fn []
                       (set-lists-loading?! true)
                       (-> (list-service/get-lists)
                           (.then (fn [data]
                                    (set-lists-state! data)
                                    (set-lists-loading?! false)))
                           (.catchError (fn [err]
                                          (println "Load lists failed:" err)
                                          (set-lists-loading?! false)))))
         create-list! (fn []
                        (let [name (when name-ctrl (.-text name-ctrl))
                              desc (when desc-ctrl (.-text desc-ctrl))]
                          (when (and name (not (empty? name)))
                            (-> (list-service/create-list {"name" name
                                                           "description" desc
                                                           "is_public" public?})
                                (.then (fn [list-data]
                                         (set-lists-create-name-ctrl! (m/TextEditingController.))
                                         (set-lists-create-desc-ctrl! (m/TextEditingController.))
                                         (set-lists-create-is-public?! false)
                                         (load-lists!)
                                         (m/Navigator.pop ctx)))
                                (.catchError (fn [err] (println "Create list failed:" err)))))))]

   (mongol-side-layout
    {:title "Lists"
     :min-width 70.0
     :actions [{:icon m/Icons.add
                :on-tap #(m/showDialog
                          .context ctx
                          .builder
                          (fn [dialog-ctx]
                            (dialog/vertical-custom-dialog
                             {:title "New list"
                              :body
                              (m/Column
                               .mainAxisSize m/MainAxisSize.min
                               .children
                               [(text-field/text-field
                                 {:controller name-ctrl
                                  :hint "Name"})
                                (m/SizedBox .height 16.0)
                                (text-field/text-field
                                 {:controller desc-ctrl
                                  :hint "Description"})
                                (m/SizedBox .height 16.0)
                                (m/CheckboxListTile
                                 .title (mgl/MongolText "Public")
                                 .value public?
                                 .onChanged (fn [v] (set-lists-create-is-public?! v)))])
                              :actions
                              [(m/TextButton
                                .onPressed (fn [] (m/Navigator.pop dialog-ctx))
                                .child (m/Text "Cancel"))
                               (m/TextButton
                                .onPressed (fn []
                                             (create-list!)
                                             (m/Navigator.pop dialog-ctx))
                                .child (m/Text "Create"))]})))}]
     :body
     (m/Scaffold
      .backgroundColor surface
      .body
      (cond
        is-loading?
        (m/Center .child (m/CircularProgressIndicator))

        (empty? lists)
        (m/Center .child (mgl/MongolText "Empty"))

        :else
        (horizontal-refresh
         {:on-refresh (fn [] (load-lists!))
          :item-count (count lists)
          :item-builder (fn [_ idx]
                          (list-item (nth lists idx)))
          :background-color surface
          :reverse false})))})))
